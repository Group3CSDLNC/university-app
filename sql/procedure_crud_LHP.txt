-- ============================================
-- Thêm Lớp học phần
-- ============================================
CREATE OR ALTER PROCEDURE sp_AddLopHocPhan
    @MaHP   BIGINT,
    @HocKy  INT,
    @NamHoc VARCHAR(9),
    @MaCTDT BIGINT,
    @SiSo   INT
AS
BEGIN
    SET NOCOUNT ON;

    -- Sinh sequence: lấy 3 số cuối cùng của MaLHP
    DECLARE @Seq INT;
    SELECT @Seq = ISNULL(MAX(CAST(RIGHT(CAST(MaLHP AS VARCHAR(20)), 3) AS INT)), 0) + 1
    FROM LopHocPhan
    WHERE HocKy = @HocKy
      AND NamHoc = @NamHoc
      AND MaHP IN (SELECT MaHP FROM HocPhan WHERE MaCTDT = @MaCTDT);

    DECLARE @MaLHP BIGINT;
    -- Tạo MaLHP từ CTDT + Học kỳ + Năm học + Seq
    -- Dùng CAST sang BIGINT
    SET @MaLHP = CAST(CONCAT(@MaCTDT, @HocKy, REPLACE(@NamHoc, '-', ''), FORMAT(@Seq, '000')) AS BIGINT);

    INSERT INTO LopHocPhan (MaLHP, MaHP, HocKy, NamHoc, SiSo)
    VALUES (@MaLHP, @MaHP, @HocKy, @NamHoc, @SiSo);

    SELECT @MaLHP AS MaLHP;
END;
GO

-- ============================================
-- Cập nhật Lớp học phần
-- ============================================
CREATE OR ALTER PROCEDURE sp_UpdateLopHocPhan
    @MaLHP BIGINT,
    @HocKy INT,
    @NamHoc VARCHAR(9),
    @SiSo INT
AS
BEGIN
    IF NOT EXISTS (SELECT 1 FROM LopHocPhan WHERE MaLHP = @MaLHP)
        THROW 51001, 'Lớp học phần không tồn tại', 1;

    UPDATE LopHocPhan
    SET HocKy = @HocKy, NamHoc = @NamHoc, SiSo = @SiSo
    WHERE MaLHP = @MaLHP;
END;

-- ============================================
-- Xóa Lớp học phần
-- ============================================
CREATE OR ALTER PROCEDURE sp_DeleteLopHocPhan
    @MaLHP BIGINT
AS
BEGIN
    IF NOT EXISTS (SELECT 1 FROM LopHocPhan WHERE MaLHP = @MaLHP)
        THROW 51001, 'Lớp học phần không tồn tại', 1;

    DELETE FROM LopHocPhan WHERE MaLHP = @MaLHP;
END;

CREATE OR ALTER PROCEDURE sp_GetLopHocPhan
    @HocKy INT = NULL,
    @NamHoc VARCHAR(9) = NULL,
    @MaCTDT INT = NULL,
    @MaGV INT = NULL
AS
BEGIN
    SELECT lhp.MaLHP, hp.TenHP, lhp.HocKy, lhp.NamHoc, lhp.SiSo
    FROM LopHocPhan lhp
    JOIN HocPhan hp ON lhp.MaHP = hp.MaHP
    LEFT JOIN GiangDay gd ON lhp.MaLHP = gd.MaLHP
    WHERE (@HocKy IS NULL OR lhp.HocKy = @HocKy)
      AND (@NamHoc IS NULL OR lhp.NamHoc = @NamHoc)
      AND (@MaCTDT IS NULL OR hp.MaCTDT = @MaCTDT)
      AND (@MaGV IS NULL OR gd.MaGV = @MaGV);
END;

-- ============================================
-- Lấy danh sách Lớp học phần theo Sinh viên
-- ============================================
CREATE OR ALTER PROCEDURE sp_GetLopHocPhanBySV
    @MaSV   BIGINT,
    @NamHoc NVARCHAR(9),
    @HocKy  INT,
    @MaCN   BIGINT = NULL   -- Tham số lọc chuyên ngành (NULL = bỏ lọc)
AS
BEGIN
    SET NOCOUNT ON;

    SELECT
        lhp.MaLHP,
        hp.MaHP,
        hp.TenHP,
        hp.SoTinChi,
        hp.TietLT,
        hp.TietTH,
        lhp.SiSo AS SiSoToiDa,
        COUNT(DISTINCT dk.MaSV) AS SoLuongDaDangKy,
        (lhp.SiSo - COUNT(DISTINCT dk.MaSV)) AS SlotConLai,
        lhp.NamHoc,
        lhp.HocKy,
        MIN(ph.SoPhong) AS PhongHoc,
        MIN(lh.TietBatDau) AS TietBatDau,
        MIN(lh.ThuTrongTuan) AS ThuTrongTuan,        -- Thêm thứ trong tuần
        MIN(lh.NgayBatDau)  AS NgayBatDau,           -- Thêm ngày bắt đầu
        MAX(lh.NgayKetThuc) AS NgayKetThuc,          -- Thêm ngày kết thúc
        gv.HoTen AS GiangVienChinh,
        STRING_AGG(tg.HoTen, ', ') AS TroGiang,
        cn.TenCN AS TenChuyenNganh,
        ctdt.TenCTDT AS TenChuongTrinhDaoTao,
        CASE WHEN dk_sv.MaSV IS NOT NULL THEN 1 ELSE 0 END AS IsDangKy
    FROM LopHocPhan lhp
    JOIN HocPhan hp ON lhp.MaHP = hp.MaHP
    JOIN ChuongTrinhDaoTao ctdt ON hp.MaCTDT = ctdt.MaCTDT
    JOIN ChuyenNganh cn ON ctdt.MaCN = cn.MaCN
    LEFT JOIN GiangDay gd ON gd.MaLHP = lhp.MaLHP
    LEFT JOIN NhanVien gv ON gd.MaGV = gv.MaNV
    LEFT JOIN GiangVien_TroGiang gvtg ON gvtg.MaHP = hp.MaHP
    LEFT JOIN NhanVien tg ON gvtg.MaGV_TroGiang = tg.MaNV
    LEFT JOIN LichHoc lh ON lh.MaLHP = lhp.MaLHP
    LEFT JOIN PhongHoc ph ON lh.MaPH = ph.MaPH
    LEFT JOIN DangKy dk ON dk.MaLHP = lhp.MaLHP
    LEFT JOIN DangKy dk_sv ON dk_sv.MaLHP = lhp.MaLHP AND dk_sv.MaSV = @MaSV
    WHERE lhp.NamHoc = @NamHoc
      AND lhp.HocKy  = @HocKy
      AND (@MaCN IS NULL OR ctdt.MaCN = @MaCN)
    GROUP BY lhp.MaLHP, hp.MaHP, hp.TenHP, hp.SoTinChi, hp.TietLT, hp.TietTH,
             lhp.SiSo, lhp.NamHoc, lhp.HocKy, gv.HoTen, cn.TenCN, ctdt.TenCTDT, dk_sv.MaSV
    ORDER BY lhp.MaLHP;
END;
GO

CREATE OR ALTER PROCEDURE sp_GetLopHocPhanV2
    @HocKy INT = NULL,
    @NamHoc VARCHAR(9) = NULL,
    @MaCTDT BIGINT = NULL,
    @MaGV INT = NULL,
    @MaHP BIGINT = NULL
AS
BEGIN
    SELECT
        lhp.MaLHP,
        hp.MaHP,
        hp.TenHP,
        hp.SoTinChi,
        lhp.HocKy,
        lhp.NamHoc,
        lhp.SiSo,
        ctdt.MaCTDT,
        ctdt.TenCTDT,
        cn.TenCN,
        k.TenKhoa,
        gv.MaNV AS MaGiangVien,
        gv.HoTen AS TenGiangVien,
        gd.SoTietPhanCong,
        tg.MaNV AS MaTroGiang,
        tg.HoTen AS TenTroGiang
    FROM LopHocPhan lhp
    JOIN HocPhan hp ON lhp.MaHP = hp.MaHP
    JOIN ChuongTrinhDaoTao ctdt ON hp.MaCTDT = ctdt.MaCTDT
    JOIN ChuyenNganh cn ON ctdt.MaCN = cn.MaCN
    JOIN Khoa k ON cn.MaKhoa = k.MaKhoa
    LEFT JOIN GiangDay gd ON lhp.MaLHP = gd.MaLHP
    LEFT JOIN NhanVien gv ON gd.MaGV = gv.MaNV
    LEFT JOIN GiangVien_TroGiang gvtg ON gvtg.MaHP = hp.MaHP AND gvtg.MaGV_Chinh = gv.MaNV
    LEFT JOIN NhanVien tg ON gvtg.MaGV_TroGiang = tg.MaNV
    WHERE (@HocKy IS NULL OR lhp.HocKy = @HocKy)
      AND (@NamHoc IS NULL OR lhp.NamHoc = @NamHoc)
      AND (@MaCTDT IS NULL OR hp.MaCTDT = @MaCTDT)
      AND (@MaGV IS NULL OR gd.MaGV = @MaGV)
      AND (@MaHP IS NULL OR hp.MaHP = @MaHP);
END;
GO

CREATE OR ALTER PROCEDURE sp_GetGiangVienChinhByLHP
    @MaLHP BIGINT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT
        gv.MaGV AS MaNV,
        nv.HoTen,
        nv.HocVi,
        'Giang vien' AS VaiTro,
        nv.Email
    FROM GiangDay gv
    JOIN NhanVien nv ON gv.MaGV = nv.MaNV
    WHERE gv.MaLHP = @MaLHP;
END
GO

CREATE OR ALTER PROCEDURE sp_GetTroGiangByLHP
    @MaLHP BIGINT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT
        tg.MaGV_TroGiang AS MaNV,
        nv.HoTen,
        nv.HocVi,
        'Tro giang' AS VaiTro,
        nv.Email
    FROM GiangVien_TroGiang tg
    JOIN NhanVien nv ON tg.MaGV_TroGiang = nv.MaNV
    JOIN LopHocPhan lhp ON tg.MaHP = lhp.MaHP
    WHERE lhp.MaLHP = @MaLHP;
END
GO
